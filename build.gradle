/*
 * Copyright (c) Octopus Deploy and contributors. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use
 * these files except in compliance with the License. You may obtain a copy of the
 * License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed
 * under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 */

buildscript {
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
    id 'com.diffplug.spotless' version '5.9.0'
    id 'com.github.ben-manes.versions' version '0.36.0'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'net.ltgt.errorprone' version '1.3.0'
    id "org.openapi.generator" version '5.2.0'
    id "com.jfrog.artifactory" version '4.18.0'
    id 'com.adarshr.test-logger' version '3.0.0'
    id 'maven-publish'
    id 'java'
    id 'distribution'
}

group = "com.octopus.wrappedapi"
defaultTasks 'build', 'checkLicenses', 'javadoc'

def buildAliases = ['dev': [
        'spotlessApply',
        'build',
        'checkLicenses',
        'javadoc'
]]

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'net.ltgt.errorprone'
    apply from: "${rootDir}/gradle/versions.gradle"
    apply from: "${rootDir}/gradle/check-licenses.gradle"

    version = rootProject.version

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    sourceCompatibility = 8
    targetCompatibility = 8

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://download.jetbrains.com/teamcity-repository" }
        maven { url "https://packages.jetbrains.team/maven/p/teamcity-rest-client/teamcity-rest-client" }
    }

    dependencies {
        errorprone "com.google.errorprone:error_prone_core"
        errorproneJavac("com.google.errorprone:javac:9+181-r4173-1")
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        java {
            // This path needs to be relative to each project
            target fileTree('.') {
                include '**/src/*/java/**/*.java'
                exclude '**/.gradle/**'
                exclude '**/build/generated/**'
            }
            removeUnusedImports()
            googleJavaFormat('1.7')
            importOrder 'com.octopus', 'java', ''
            trimTrailingWhitespace()
            endWithNewline()
        }

        groovyGradle {
            target '*.gradle'
            greclipse().configFile(rootProject.file('gradle/formatter.properties'))
            endWithNewline()
        }

        // Below this line are currently only license header tasks
        format 'groovy', { target '**/src/*/grovy/**/*.groovy' }
    }

    tasks.withType(JavaCompile) {
        doFirst {
            logger.info("From Parent")
        }
        options.compilerArgs += [
                '-Xlint:unchecked',
                '-Xlint:cast',
                '-Xlint:rawtypes',
                '-Xlint:overloads',
                '-Xlint:divzero',
                '-Xlint:finally',
                '-Xlint:static',
                '-Werror',
        ]

        options.errorprone {
            excludedPaths = '.*generated/*.*'

            // Our equals need to be symmetric, this checker doesn't respect that.
            check('EqualsGetClass', CheckSeverity.OFF)
            // We like to use futures with no return values.
            check('FutureReturnValueIgnored', CheckSeverity.OFF)
            // We use the JSR-305 annotations instead of the Google annotations.
            check('ImmutableEnumChecker', CheckSeverity.OFF)
            // This is a style check instead of an error-prone pattern.
            check('UnnecessaryParentheses', CheckSeverity.OFF)

            // This check is broken in Java 12.  See https://github.com/google/error-prone/issues/1257
            if (JavaVersion.current() == JavaVersion.VERSION_12) {
                check('Finally', CheckSeverity.OFF)
            }
            // This check is broken after Java 12.  See https://github.com/google/error-prone/issues/1352
            if (JavaVersion.current() > JavaVersion.VERSION_12) {
                check('TypeParameterUnusedInFormals', CheckSeverity.OFF)
            }

            check('FieldCanBeFinal', CheckSeverity.WARN)
            check('InsecureCryptoUsage', CheckSeverity.WARN)
            check('WildcardImport', CheckSeverity.WARN)
        }
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
        testLogging.showStandardStreams = true
    }

    if (project.name != "acceptance-tests") {
        apply plugin: "com.jfrog.artifactory"
        apply plugin: 'maven-publish'

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    groupId = "com.octopus"
                    version "${project.version}"
                    artifact jar
                    artifact sourcesJar
                    artifact javadocJar
                }
            }
        }
    }
}


apply plugin: "org.openapi.generator"
project(":octopus-openapi-wrapper").getTasks().getByName("compileJava").dependsOn tasks.openApiGenerate

openApiGenerate {
    generatorName = "java"
    validateSpec = false
    skipValidateSpec = true

    // Ignore files does not alter behaviour
    ignoreFileOverride = project(":octopus-openapi-wrapper").projectDir.toPath().resolve(".openapi-generator-ignore").toString()
    //inputSpec = project(":octopus-openapi-wrapper").projectDir.toPath().resolve("spec").resolve("swagger.json")
.toString()
    inputSpec = project(":octopus-openapi-wrapper").projectDir.toPath().resolve("spec").resolve("new.json").toString()
    templateDir = project(":octopus-openapi-wrapper").projectDir.toPath().resolve("templates").toString()
    outputDir = project(":octopus-openapi-wrapper").buildDir.toPath().resolve("generated").toString()
    apiPackage = "com.octopus.openapi.api"
    modelPackage = "com.octopus.openapi.model"
    invokerPackage = "com.octopus.openapi.invoker"
    groupId = "com.octopus"
    generateModelTests = false
    generateModelDocumentation = false
    generateApiTests = false
    generateApiDocumentation = false
    id = "sdk-api"
    configOptions = [
            dateLibrary: "java8",
            licenseName: "APache 2.0",
            licenseUrl : "https://www.apache.org/licenses/LICENSE-2.0.txt"
    ]
}

jar { enabled = false }


def artifactoryUser = project.hasProperty('artifactoryUser') ? project.property('artifactoryUser') : System.getenv('ARTIFACTORY_USER')
def artifactoryPassword = project.hasProperty('artifactoryApiKey') ? project.property('artifactoryApiKey') :
        System.getenv('ARTIFACTORY_PASSWORD')

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver

    publish {
        repository {
            repoKey = 'maven'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
}
//
//artifactoryPublish {
//  dependsOn jar
//}
