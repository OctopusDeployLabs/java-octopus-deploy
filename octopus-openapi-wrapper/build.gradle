apply plugin: 'java'
apply plugin: "org.openapi.generator"

tasks.named('jar') {
  archiveBaseName = 'octopus-sdk-api'
  manifest {
    attributes(
      'Specification-Title': archiveBaseName,
      'Specification-Version': project.version,
      'Implementation-Title': archiveBaseName,
      'Implementation-Version': project.version
      )
  }
}

sourceSets.main.java.srcDirs += "build/generated/src/main/java"

compileJava {
  // Ignore warnings raised by OpenApiGenerated code
  options.compilerArgs -= [
    '-Xlint:rawtypes',
    '-Xlint:cast',
    '-Werror'
  ]
  doFirst {
    logger.error('An error log message.')
  }

  doLast {
    logger.info("Java warnings have been disabled for compilation of openapigenerateor generated coded.")
  }
}


openApiGenerate {
  generatorName = "java"
  validateSpec = false
  skipValidateSpec = true

  // Ignore files does not alter behaviour
  ignoreFileOverride = ".openapi-generator-ignore"
  inputSpec = "${projectDir}/spec/swagger.json"
  templateDir = "${projectDir}/templates"
  outputDir = "${buildDir}/generated"
  apiPackage = "com.octopus.openapi.api"
  modelPackage = "com.octopus.openapi.model"
  invokerPackage = "com.octopus.openapi.invoker"
  groupId = "com.octopus"
  generateModelTests = false
  generateModelDocumentation = false
  generateApiTests = false
  generateApiDocumentation = false
  id = "sdk-api"
  configOptions = [
    dateLibrary: "java8",
    licenseName: "APache 2.0",
    licenseUrl : "https://www.apache.org/licenses/LICENSE-2.0.txt"
  ]
}

dependencies {
  implementation 'io.swagger:swagger-annotations'
  implementation "com.google.code.findbugs:jsr305"
  implementation 'com.squareup.okhttp3:okhttp'
  implementation 'com.squareup.okhttp3:logging-interceptor'
  implementation 'com.google.code.gson:gson'
  implementation 'io.gsonfire:gson-fire'
  implementation group: 'org.apache.commons', name: 'commons-lang3'
  implementation 'io.swagger.parser.v3:swagger-parser-v3'
  implementation 'javax.annotation:javax.annotation-api'
}

javadoc {
  options.tags = [
    "http.response.details:a:Http Response Details"
  ]
}
